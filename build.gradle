apply plugin: 'java-library'

import org.gradle.internal.os.OperatingSystem

def generatedJavaSourceDir = 'src-gen/main/java'
def generatedDistDir = 'src-gen/main/dist'

repositories {
    jcenter()
    mavenCentral()
}

//group = 'de.nerogar'
version = '0.1.0'
sourceCompatibility = 1.8

// lwjgl

project.ext.lwjglVersion = "3.1.2"

def osProp = System.getProperty("os")
def os = osProp == null ? OperatingSystem.current() : OperatingSystem.forName(osProp)

switch (os) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    default:
        throw new UnsupportedOperationException("Unsupported OS: " + os.familyName + "(" + osProp + ")")
}

dependencies {
    // lwjgl
    compile "org.lwjgl:lwjgl:${lwjglVersion}"
    runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
    compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
    compile "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
    runtime "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:${lwjglNatives}"

    // openGL, openAL, openCL
    compile "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
    runtime "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}"
    compile "org.lwjgl:lwjgl-openal:${lwjglVersion}"
    runtime "org.lwjgl:lwjgl-openal:${lwjglVersion}:${lwjglNatives}"
    compile "org.lwjgl:lwjgl-opencl:${lwjglVersion}"

    // stb
    compile "org.lwjgl:lwjgl-stb:${lwjglVersion}"
    runtime "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"

    if (findProject(':annotation')) {
        compile project(':annotation')
    }

    // TODO this should only be included as an annotationProcessor
    // it looks like IntelliJ does not support this yet and will randomly not find the processor
    // see https://youtrack.jetbrains.com/issue/IDEA-187868
    if (findProject(':annotationProcessor')) {
        compile project(':annotationProcessor')
    }

    testImplementation 'junit:junit:4.12'
}

// generated resources
compileJava {
    // IntelliJ does not auto detect this
    // see https://youtrack.jetbrains.com/issue/IDEA-182577
    options.annotationProcessorGeneratedSourcesDirectory = file(generatedJavaSourceDir)
}

processResources {
    from(generatedDistDir) {
        into 'noiseEngine/'
    }
}

/*
task copyResGen(type: Copy) {
    from 'src-gen/main/dist'
    into 'src/main/resources/noiseEngine'
}
*/
